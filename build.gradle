plugins {
	id 'org.ajoberstar.grgit' version '1.4.2'
	id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'scala'

sourceSets {
	main {
		scala.srcDirs = ['src/main/scala', 'src/main/java']
		java.srcDirs = []
	}
}

import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

version = generateVersion('0.2')
String pluginMain = 'com.lesserhydra.resmagna.ResMagna'

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
	}
}

dependencies {
	compile 'org.scala-lang:scala-library:2.12.+'
	compile 'org.scala-lang:scala-compiler:2.12.+'
	compile 'org.scala-lang:scala-reflect:2.12.+'

	compile 'org.jetbrains:annotations:15.0'
	compile 'org.spigotmc:spigot:1.11-R0.1-SNAPSHOT'
	compile 'org.bukkit:craftbukkit:1.11-R0.1-SNAPSHOT'

	testCompile 'junit:junit:4.12'
	testCompile 'org.powermock:powermock-api-mockito:1.6.+'
	testCompile 'org.powermock:powermock-module-junit4:1.6.+'
	testCompile 'org.mockito:mockito-all:1.10.+'
}

shadowJar {
	dependencies {
		include(dependency('org.scala-lang:scala-library:2.12.+'))
		include(dependency('org.scala-lang:scala-compiler:2.12.+'))
		include(dependency('org.scala-lang:scala-reflect:2.12.+'))
	}
}

//Generate plugin.yml
processResources {
	inputs.file 'build.gradle' //So it's regenerated if version changes
	from 'templates/plugin.yml'
	filter(ReplaceTokens, tokens: [
			version:		version,
			mainClass:  pluginMain,
	])
}

static String generateVersion(workingVersion) {
	Grgit repo = Grgit.open()
	Tag currentTag = repo.tag.list().find {it.commit == repo.head()}

	if (currentTag == null) return workingVersion + '-SNAPSHOT'
	return currentTag.getName()
}
